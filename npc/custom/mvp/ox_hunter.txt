//===== Hercules Script ======================================
//= Ox Hunter - Year of the Ox Commemoration
//===== By: ==================================================
//= Ozcodex
//===== Current Version: =====================================
//= 1.0
//===== Description: =========================================
//= Special event NPC commemorating the Year of the Ox.
//= An expert MVP hunter who has traveled to Rune Midgard
//= to hear tales of powerful beasts defeated.
//= Rewards players with Ox Coins based on their MVP stats.
//===== Additional Comments: =================================
//= 1.0 Initial version. Spanish dialogue, English code.
//=     - Rewards: 5 coins per MVP kill, 1 coin per party assist
//=     - Tracks total coins given to prevent infinite rewards
//============================================================

geffen_in,82,62,3	script	Ushi the Ox Hunter	4_M_COWRAIDERS3,{
	function CalculateMVPReward;
	
	mes "^8B4513[Ushi el Cazador]^000000";
	mes "Saludos, aventurero. Soy Ushi, un cazador que ha recorrido";
	mes "tierras lejanas en busca de las bestias mas poderosas.";
	next;
	
	mes "^8B4513[Ushi el Cazador]^000000";
	mes "Estamos celebramos al Buey, simbolo de fuerza y tenacidad.";
	mes "He venido a Rune Midgard para escuchar las historias";
	mes "de cazadores valientes como tu.";
	next;
	
	switch(select("Contar mis historias de caza:Preguntar sobre recompensas:Despedirse")) {
		case 1:
			CalculateMVPReward();
			break;
		case 2:
			mes "^8B4513[Ushi el Cazador]^000000";
			mes "Ah, te interesa mi oferta? Excelente!";
			next;
			mes "^8B4513[Ushi el Cazador]^000000";
			mes "Por cada bestia poderosa que hayas vencido,";
			mes "ya sea dando el golpe final o apoyando a tu party,";
			mes "te dare un numero especifico de Ox Coins.";
			next;
			mes "^8B4513[Ushi el Cazador]^000000";
			mes "Lo importante es que participaste en la batalla!";
			mes "Sin embargo, solo recompensare tus hazañas una vez.";
			next;
			mes "^8B4513[Ushi el Cazador]^000000";
			mes "Las monedas que ya te he entregado: ^FF0000" + #OX_COINS_GIVEN + "^000000";
			break;
		case 3:
			mes "^8B4513[Ushi el Cazador]^000000";
			mes "Que los vientos te sean favorables, cazador.";
			mes "Regresa cuando tengas mas historias que compartir.";
			break;
	}
	close;

// Function to calculate MVP rewards
function CalculateMVPReward {
	// MVP array with coin rewards: MVP_ID, coins, MVP_ID, coins, etc
	// Each MVP gives the same coins whether you killed it or supported
	setarray .@mvp_rewards[0],
		1832, 100,  //Ifrit
		1648, 100,  //MasterSmith Howard
		1719, 90,  //Detardeurus
		1871, 90,  //Falling Bishop
		2202, 90,  //Kraken
		1917, 100,  //Wounded Morroc
		1874, 100,  //Beelzebub
		1649, 96,  //High Priest 
		1646, 93,  //Lord Knight Seyren
		1651, 90,  //High Wizard Kathryne
		2476, 85,  //Amdarais
		1647, 84,  //Assassin Cross Eremes
		1650, 82,  //Sniper Cecil
		2319, 81,  //Buwaya
		2483, 80,  //Nightmare Baphomet
		1685, 76,  //Vesper
		2022, 69,  //Nidhoggur's Shadow
		1751, 80,  //Valkyrie Randgris
		2341, 64,  //2011 RWC Boss
		1768, 60,  //Gloom Under Night
		1779, 52,  //Ktullanux
		2362, 50,  //Amon Ra - Nightmare
		2251, 50,  //Gioia
		2255, 50,  //Dark Guardian Kades
		1734, 50,  //Kiel D-01
		2253, 50,  //General Daehyon
		2087, 45,  //Queen Scaraba
		2249, 44,  //Angry Student Pyuriel
		2475, 36,  //Corrupted Soul
		1785, 30,  //Atroce
		1708, 35,  //Memory of Thanatos
		1312, 28,  //Turtle General
		2068, 25,  //Boitata
		1252, 25,  //Hatii
		1583, 25,  //Tao Gunka
		1272, 23,  //Dark Lord
		1038, 23,  //Osiris
		1885, 22,  //Gopinch
		1511, 20,  //Amon Ra
		1623, 20,  //RSX-0806
		1492, 19,  //Samurai 
		1157, 18,  //Pharaoh
		1112, 16,  //Drake
		1630, 14,  //White Lady
		1039, 15,  //Baphomet
		1980, 12,  //Kublin
		1251, 12,  //Stormy 
		1373, 12,  //Lord the Dead
		1190, 7,  //Orc Lord
		1046, 10,  //Doppelganger
		1147, 7,  //Maya
		1059, 10,  //Mistress
		1087, 7,  //Orc Hero
		1688, 10,  //Lady Tanee
		1389, 10,  //Dracula
		1150, 6,  //Moonlight Flower
		1159, 6,  //Phreeoni
		1086, 4;  //Golden Thief Bug
	
	.@total_coins = 0;
	.@array_size = getarraysize(.@mvp_rewards);
	
	// Calculate total coins from all MVP participations
	for (.@i = 0; .@i < .@array_size; .@i += 2) {
		.@mvp_id = .@mvp_rewards[.@i];
		.@coin_value = .@mvp_rewards[.@i + 1];
		.@kill_var$ = "MVP_KILL_" + .@mvp_id;
		.@supp_var$ = "MVP_SUPP_" + .@mvp_id;
		.@kill_count = getd(.@kill_var$);
		.@supp_count = getd(.@supp_var$);
		.@total_coins += (.@kill_count + .@supp_count) * .@coin_value;
	}
	
	mes "^8B4513[Ushi el Cazador]^000000";
	
	// Check if player has any MVP stats
	if (.@total_coins == 0) {
		mes "Hmm... veo que aun no has enfrentado bestias poderosas.";
		mes "Regresa cuando hayas forjado tu leyenda, joven cazador.";
		return;
	}
	
	// Check how many coins already given
	.@coins_pending = .@total_coins - #OX_COINS_GIVEN;
	
	if (.@coins_pending <= 0) {
		mes "Ah, ya has reclamado todas tus recompensas, noble cazador.";
		mes "No tengo mas monedas que ofrecerte por ahora.";
		next;
		mes "^8B4513[Ushi el Cazador]^000000";
		mes "Monedas ya entregadas: ^009900" + #OX_COINS_GIVEN + "^000000";
		mes "Sigue cazando para aumentar tu leyenda!";
		return;
	}
	
	// Show stats
	mes "Impresionante! Dejame ver tus logros:";
	next;
	
	mes "^8B4513[Ushi el Cazador]^000000";
	mes "^777777Tus hazañas de caza^000000";
	mes " ";
	mes "^0000FFRecompensa total:^000000 " + .@total_coins + " monedas";
	mes "^FF0000Ya reclamadas:^000000 " + #OX_COINS_GIVEN + " monedas";
	mes "^00FF00Por reclamar:^000000 " + .@coins_pending + " monedas";
	next;
	
	mes "^8B4513[Ushi el Cazador]^000000";
	mes "Eres un guerrero digno de admiracion!";
	mes "Aceptaras mi humilde ofrenda de " + .@coins_pending + " Ox Coins?";
	next;
	
	if(select("Aceptar las monedas:Rechazar por ahora") == 2) {
		mes "^8B4513[Ushi el Cazador]^000000";
		mes "Como desees. Las monedas te esperaran cuando estes listo.";
		return;
	}
	
	// Give coins
	getitem 7606, .@coins_pending;
	#OX_COINS_GIVEN += .@coins_pending;
	
	mes "^8B4513[Ushi el Cazador]^000000";
	mes "Que estas monedas te sirvan en tus futuras aventuras!";
	mes "Tus historias seran recordadas en mi tierra natal.";
	next;
	
	mes "^8B4513[Ushi el Cazador]^000000";
	mes "Sigue cazando, guerrero. Cada nueva victoria";
	mes "te traera mas recompensas cuando regreses.";
	
	return;
}
}
